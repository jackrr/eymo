use crate::lang::ast::{Statement, Transform, Shape, FaceRef, FaceIdx, FacePart, Operation, FlipVariant};
use crate::shapes::rect::Rect;
use std::str::FromStr;

grammar;

match {
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
		r"[0-9]+" => UINT,
} else {
    r"-?[0-9]+" => SINT
} else {
    r"-?[0-9]+\.[0-9]*" => F32,
		_
}

pub Statements = WhitespaceDelim<Statement>;

pub Statement: Statement = {
		<t:Transform> => Statement::Transform(<>),
};

Transform: Transform = {
    <shape:Shape> r": *" <operations:Operations> => Transform {<>}
};

Shape: Shape = {
		"rect(" <x:Num> <y:Num> <w:Num> <h:Num> ")" => Shape::Rect(Rect::from_tl(x, y, w, h)),
		<part:FacePart> "#" <face_idx:Num> => Shape::FaceRef(FaceRef {part, face_idx: Some(FaceIdx::Absolute(face_idx))}),
		<part:FacePart> "+" <face_rel:Num> => Shape::FaceRef(FaceRef {part, face_idx: Some(FaceIdx::Relative(face_rel as i32))}),
		// FIXME: -NN parsing is borked
		// <part:FacePart> "-" <face_rel:Num> => Shape::FaceRef(FaceRef {part, face_idx: Some(FaceIdx::Relative(-1 * face_rel as i32))}),
    <part:FacePart> => Shape::FaceRef(FaceRef { part, face_idx: None }),
};

FacePart: FacePart = {
		"leye" => FacePart::LEye,
		"reye" => FacePart::REye,
		"leye_region" => FacePart::LEyeRegion,
		"reye_region" => FacePart::REyeRegion,
		"face" => FacePart::Face,
		"nose" => FacePart::Nose,
		"mouth" => FacePart::Mouth,
		"forehead" => FacePart::Forehead,
};

Operations = CommaDelim<Operation>;

Operation: Operation = {
    "tile" => Operation::Tile,
		"scale(" <f:Float> ")" => Operation::Scale(f),
		"rotate(" <f:Float> ")" => Operation::Rotate(f),
		"copy_to(" <s:CommaDelim<Shape>> ")" => Operation::CopyTo(s),
		"swap_with(" <s:Shape> ")" => Operation::SwapWith(s),
		"translate(" <x:Int> r" *, *" <y:Int> ")" => Operation::Translate(<>),
		"flip(" <fv:FlipVariant> ")" => Operation::Flip(fv),
		"drift" => Operation::Drift(0.5, 45.),
		"drift(" <a:Float> ")" => Operation::Drift(a, 45.),
		"drift(" <a:Float> r" *, *" <v:Float> ")" => Operation::Drift(<>),
		"spin" => Operation::Spin(0.5),
		"spin(" <v:Float> ")" => Operation::Spin(v),
		"brighten(" <f:Float> ")" => Operation::Brightness(f),
		"saturate(" <f:Float> ")" => Operation::Saturation(f),
		"channels(" <r:Float> r" *, *" <g:Float> r" *, *" <b:Float> ")" => Operation::Chans(<>),
};

FlipVariant: FlipVariant = {
    "both" => FlipVariant::Both,
		"vertical" => FlipVariant::Vertical,
		"horizontal" => FlipVariant::Horizontal,
};

Num: u32 = {
		UINT => u32::from_str(<>).unwrap(),
};

Int: i32 = {
		SINT => i32::from_str(<>).unwrap(),
		UINT => i32::from_str(<>).unwrap(),
};

Float: f32 = {
    F32 => f32::from_str(<>).unwrap(),
		SINT => f32::from_str(<>).unwrap(),
		UINT => f32::from_str(<>).unwrap(),
};

CommaDelim<T>: Vec<T> = {
    <mut v:(<T> r" *, *")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

WhitespaceDelim<T>: Vec<T> = {
    r"\s*"? <mut v:(<T> r"\s*")*> <e:T?> r"\s*" => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}
